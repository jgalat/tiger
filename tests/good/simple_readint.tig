let
  type any = {any : int}
  var buffer := getchar ()

  function readint (any : any) : int =
    let var i := 0
        function isdigit (s : string) : int =
          ord ("0") <= ord (s) & ord (s) <= ord ("9")
        function skipto () =
          while buffer = " " | buffer = "\n"
            do buffer := getchar ()
    in skipto ();
       any.any := isdigit (buffer);
       while isdigit (buffer)
         do (i := i * 10 + ord (buffer) - ord ("0");
             buffer := getchar ());
       i
    end

  function printint (i : int) =
    let function f (i : int) =
         if i > 0
 	then (f (i/10); print (chr (i-i/10*10+ord ("0"))))
    in if i < 0 then (print ("-"); f (-i))
       else if i>0 then f (i)
       else print ("0")
    end

  var any : any := any {any = 0}
  var i := readint(any)
in
  printint(i); print("\n"); 0
end
